#cpd /xw
#abund wilms
query yes

data 1:1 0087940201/odf/repro/mos1S001-bkg-grp50.pi 
data 2:2 0087940201/odf/repro/mos2S002-bkg-grp50.pi 
data 3:3 0087940201/odf/repro/pnS003-bkg-os-grp50.pi

resp 1 0087940201/odf/repro/mos1S001-bkg.rmf
resp 2 0087940201/odf/repro/mos2S002-bkg.rmf
resp 3 0087940201/odf/repro/pnS003-bkg.rmf

arf  1 0087940201/odf/repro/mos1S001-bkg.arf
arf  2 0087940201/odf/repro/mos2S002-bkg.arf
arf  3 0087940201/odf/repro/pnS003-bkg.arf

back 1 0087940201/odf/repro/mos1S001-bkg-qpb.pi
back 2 0087940201/odf/repro/mos2S002-bkg-qpb.pi
back 3 0087940201/odf/repro/pnS003-bkg-qpb.pi

# the 1:1/1:2/1:3 resp/arf commands are optional
# but helpful for clarity

#resp 1:1 0087940201/odf/repro/mos1S001-bkg.rmf
#arf  1:1 0087940201/odf/repro/mos1S001-bkg.arf
resp 2:1 0087940201/odf/repro/mos1S001-bkg.rmf
arf  2:1 0087940201/odf/repro/mos1S001-bkg.arf
resp 3:1 caldb/mos1-diag.rsp 
arf  3:1 none
resp 6:1 0087940201/odf/repro/mos1S001-bkg.rmf
arf  6:1 0087940201/odf/repro/mos1S001-bkg.arf

#resp 1:2 0087940201/odf/repro/mos2S002-bkg.rmf
#arf  1:2 0087940201/odf/repro/mos2S002-bkg.arf
resp 2:2 0087940201/odf/repro/mos2S002-bkg.rmf
arf  2:2 0087940201/odf/repro/mos2S002-bkg.arf
resp 4:2 caldb/mos2-diag.rsp 
arf  4:2 none
resp 6:2 0087940201/odf/repro/mos2S002-bkg.rmf
arf  6:2 0087940201/odf/repro/mos2S002-bkg.arf

#resp 1:3 0087940201/odf/repro/pnS003-bkg.rmf
#arf  1:3 0087940201/odf/repro/pnS003-bkg.arf
resp 2:3 0087940201/odf/repro/pnS003-bkg.rmf
arf  2:3 0087940201/odf/repro/pnS003-bkg.arf
resp 5:3 caldb/pn-diag.rsp 
arf  5:3 none
resp 6:3 0087940201/odf/repro/pnS003-bkg.rmf
arf  6:3 0087940201/odf/repro/pnS003-bkg.arf

# To-do: script the starting parameters
# con * con should be different for each instrument
# Relative normalization of apec/po should be same for given obsid
# Normalizations of gaussian lines should be same-ish...

model 1:xrb con*con*(apec + phabs*(po+apec))
    1
    1
    1
    1
    0
    2e-5
    1
    1.4
    1e-5
    0.3
    1
    0
    1e-4

# TODO I think this should be 1.85 keV, not 1.75 keV...

model 2:instr gauss + gauss + gauss + gauss + gauss + gauss + gauss + gauss
    1.49
    0
    0
    1.75
    0
    0
    1.49
    0
    0
    7.49
    0
    0
    7.11
    0
    0
    8.05
    0
    0
    8.62
    0
    0
    8.90
    0
    0
model 3:spm1 po
    0.7
    1e-2
model 4:spm2 po
    0.7
    1e-2
model 5:sppn po
    0.7
    1e-2

### Instrumental line parameters ###

# Freeze all parameters (fix energies + linewidths)
freeze instr:1-24

# Set MOS line normalizations, but hold to zero for PN fit
newpar instr:3 1e-5
newpar instr:6 1e-5
thaw instr:3,6
newpar instr:27 1e-5
newpar instr:30 1e-5
# thaw instr:27,30
# implicit thaw
# But, hold at zero for PN fit
newpar instr:51 0
newpar instr:54 0
freeze instr:51,54

# May be prudent to unfreeze line normalizations for MOS1 vs. MOS2 and see what
# happens.  Or, free line energies

# Set PN line normalizations; MOS fits not affected
newpar instr:57 1e-5
newpar instr:60 1e-5
newpar instr:63 1e-5
newpar instr:66 1e-5
newpar instr:69 1e-5
newpar instr:72 1e-5

### X-ray background parameters ###

# local bubble kT, halo kT, absorption nH, powerlaw alpha
freeze xrb:3,7,8,10

# Constant terms.  Normalize to MOS1 fit
freeze xrb:1
freeze xrb:2,15,28
# Let one const term float for MOS2, PN fits
newpar xrb:14 1
newpar xrb:27 1

# Only thawed xrb parameters are 6,9,13 (apec,powerlaw,apec norm)
# 14,27 (constant shift terms for MOS2/PN fits)

### Soft proton parameters ###
freeze spm1:1
freeze spm2:1
freeze sppn:1

### SWCX??? ###
model 6:swcx gauss+gauss
    0.56
    0
    1e-5
    0.65
    0
    1e-5
freeze swcx:1,2,4,5
# If you want to let PN normalizations float relative to MOS
# but I don't think it should matter
# nor should there be a physical reason to let this happen
# (as differences in MOS/PN sensitivity to SWCX emission should be accounted
# for by RMF/ARF)
# newpar swcx:15 1e-5
# newpar swcx:18 1e-5

setplot en
notice all
ignore 1:**-0.3,11.0-**
ignore 2:**-0.3,11.0-**
ignore 3:**-0.4,11.0-**
# XMM ESAS cookbook ignores PN to 0.4keV... I wonder why?
# Can this be justified from physical grounds?  Ensure we're not losing signal.

fit

thaw spm1:1
thaw spm2:1
thaw sppn:1

fit

# Note : I did a bunch more hand modifications to...
# let kT,nH values float a bit
# use 1.85keV instead of 1.75keV for MOS Si line(s)
# that's really it.

thaw xrb:3,7,10

fit

#save all backfit.xcm
