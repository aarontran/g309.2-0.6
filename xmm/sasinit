#!/bin/tcsh
#
# Wrapper script to set XMM-Newton SAS software environment,
# whether for interactive or script work
#
# Expected result: most recent versions of HEASOFT and XMM SAS initialized.
# XMM SAS scripts, FTOOLS, etc accessible via $PATH.
# Environment variables SAS_CCFPATH and XMM_PATH set.
#
# If obsid provided, environment variables SAS_ODF and SAS_CCF set
# (requires cifbuild, odfingest already run)
#
# Paths to HEASOFT, SAS installs must be updated if ported to other computers
#
# A. Tran, 2015 Sep 21

# WARNING: $PATH and $path are the same list, respectively colon/space
# delimited, but `set path = (... $path)` updates the environment variable
# whereas this is not the case for $PATH...
# something weird specific to HEAD machines?...

set path = (/soft/saord/bin $path)

set MACHINE = `uname -m`
switch ($MACHINE)
  case i686
    setenv HEADAS /soft/lheasoft/headas/i686-pc-linux
    source $HEADAS/headas-init.csh
  breaksw
  case x86_64
    setenv HEADAS /soft/lheasoft/headas/x86_64-pc-linux
    source $HEADAS/headas-init.csh
  breaksw
endsw

source /soft/XMM/xmmsas/setsas.csh

setenv SAS_CCFPATH /proj/xmm/ccf
echo "setenv SAS_CCFPATH $SAS_CCFPATH"

# Project path with obsid folders, e.g. $XMM_PATH/0087940201/odf/repro/

setenv XMM_PATH /data/gas2/atran/g309/xmm
echo "setenv XMM_PATH $XMM_PATH"
set path = ($XMM_PATH $path)
set path = ($XMM_PATH/bin $path)
set path = ($XMM_PATH/bin/snippets $path)
set path = ($XMM_PATH/bin/unused $path)

echo "setenv PYTHONPATH \${PYTHONPATH}:${XMM_PATH}/bin"
echo "setenv PYTHONPATH \${PYTHONPATH}:${XMM_PATH}/bin-image"
setenv PYTHONPATH "${PYTHONPATH}:${XMM_PATH}/bin"
setenv PYTHONPATH "${PYTHONPATH}:${XMM_PATH}/bin-image"

set obsid = $1
if ( $obsid != "") then
    setenv SAS_OBSID $obsid
    setenv SAS_REPRO $XMM_PATH/$obsid/repro
    setenv SAS_CCF $SAS_REPRO/ccf.cif
    setenv SAS_ODF `ls -1 $SAS_REPRO/*SUM.SAS`
    echo "setenv SAS_OBSID $SAS_OBSID"
    echo "setenv SAS_REPRO $SAS_REPRO"
    echo "setenv SAS_CCF $SAS_CCF"
    echo "setenv SAS_ODF $SAS_ODF"
    echo ""
    if ($SAS_ODF != "") then
	echo "XMM SAS ready for $obsid"
    else
    	echo "ERROR: $SAS_REPRO/*SUM.SAS not found!"
    	echo "       Did you run sasrepro?"
	exit 1
    endif
endif

set prompt="%{\e[32;1m%}%n(sas)%{\e[37m%}@%{\e[33m%}%m%{\e[37m%}:%{\e[36m%}%~%{\e[37m%}"\$"%{\e[0m%} "
